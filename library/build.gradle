plugins {
    id "com.jfrog.bintray" version "1.6"
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

init()

group = 'cn.okayj'
version = '1.0.1'
archivesBaseName = 'ax-recyclerview'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def siteUrl = 'https://github.com/jack-cook/axRecyclerView'
def gitUrl = 'https://github.com/jack-cook/axRecyclerView.git'

bintray {
    user = local.getAt("BINTRAY_USER");
    key = local.getAt('BINTRAY_KEY');

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'axRecyclerView'
        licenses = ['Apache-2.0']

        websiteUrl = siteUrl
        vcsUrl = gitUrl

        version {
            name = project.version
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'axRecyclerView'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'okayj'
                        name 'Jack Cook'
                        email '497737103@qq.com'
                    }
                }
            }
        }
    }
}

void init(){
    Properties localProperties = new Properties();
    localProperties.load(new FileInputStream(file('../local.properties')));
    ext {
        local = localProperties;
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:recyclerview-v7:25.+'
    compile 'com.android.support:support-annotations:25.+'
}
